plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
    id 'java'
    id 'application'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.5.4.RELEASE'
    }
}

mainClassName = "com.example.Application"

task copyDockerFile(type: Copy) {
    dependsOn installDist
    from 'src/main/docker/Dockerfile'
    into installDist.destinationDir
}

assemble.dependsOn installDist
assemble.dependsOn copyDockerFile

distZip.enabled = false
distTar.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

ext {
    dockerImageTag = "remen/${applicationName}:latest"
}

task buildDockerImage(type: Exec) {
    group 'Build'
    description 'Builds a docker image for this project'

    dependsOn installDist, copyDockerFile
    commandLine 'docker', 'build', installDist.destinationDir, '-t', dockerImageTag
}

task pushDockerImage(type: Exec) {
    group 'Distribution'
    description 'Pushes the docker image to the repository'

    dependsOn buildDockerImage
    commandLine 'docker', 'push', dockerImageTag
}