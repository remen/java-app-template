plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.1.3-2"
    id 'java'
    id 'application'
}

ext {
    kotlin_version = "1.1.3-2"
    dockerImageTag = "remen/${applicationName}:latest"
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.5.4.RELEASE'
    }
}

mainClassName = "com.example.MainKt"

task copyDockerFile(type: Copy) {
    dependsOn installDist
    from 'src/main/docker/Dockerfile'
    into installDist.destinationDir
}

assemble.dependsOn installDist
assemble.dependsOn copyDockerFile

distZip.enabled = false
distTar.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

task buildDockerImage(type: Exec) {
    group 'Build'
    description 'Builds a docker image for this project'

    dependsOn installDist, copyDockerFile
    commandLine 'docker', 'build', installDist.destinationDir, '-t', dockerImageTag
}

task pushDockerImage(type: Exec) {
    group 'Distribution'
    description 'Pushes the docker image to the repository'

    dependsOn buildDockerImage
    commandLine 'docker', 'push', dockerImageTag
}